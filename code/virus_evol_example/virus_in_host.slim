// -------------------
// WITHIN-HOST MODEL
// -------------------

// INITIALIZE
initialize() {
	initializeSLiMModelType("nonWF");
	initializeSLiMOptions(keepPedigrees = T);
	initializeTreeSeq();
	defaults = Dictionary(
		"SEED", getSeed(),
		"INFILE", "",                  // optional file to read in start from
		"N_FOUNDERS", 20,              // average number chosen to start a new infection
		"REPRODUCTION_BENEFIT", 1.0,   // increase in reproduction rate for m2 mutations
		"TRANSMISSION_BENEFIT", 1e5,   // increase in transmission probability for m1 mutations
		"TRANSMISSION_DAY", 50,        // on which day does transmission happen
		"OVERALL_DAY", 25.0,           // on which day does the sim finish
		"OUTPUT_DIR", ".",             // output directory
		"OUTPUT_HOST_IDS", "OUT1;OUT2",  // IDs of hosts to output infections for, ';'-separated
		"HOST_ID", "TEST",             // this host ID
		"INF_ID", "inf0",       		 // another ID
		"POP_GROWTH_RATE", 1.0,        // rate of growth of population
		"K", 1e5     				       // carrying capacity within host
		);
	
	// Set up parameters with a user-defined function
	setupParams(defaults);
	defineConstant("PARAMS", defaults);

	initializeChromosome(1, 1, "H");
	initializeRecombinationRate(0);
	initializeMutationRate(1e-4);
	initializeMutationType("m1", 0, "f", 0); // improve transmission
	initializeMutationType("m2", 0, "f", 0); // improve reproduction
	c(m1,m2).convertToSubstitution = F;
	// enforce mutually exclusive replacement for derived/ancestral mutations
	c(m1,m2).mutationStackGroup = 1;
	c(m1,m2).mutationStackPolicy = 'l';
	initializeGenomicElementType("g1", c(m1, m2), c(1, 1));
	defineConstant("MUT_POS", 0);
	initializeGenomicElement(g1, MUT_POS, MUT_POS);

	// print start message
	catn("\n\nWITHIN-HOST PARAMETER SUMMARY:\n");
}

// Pathogens infect host
1 first() {
   // start seed simulation or load an input file
   if (INFILE == "") {
      catn("\nSTARTING SEED SIMULATION FROM " + INFILE + "\n");
      // infections are started with X pathogens, on average, where X = N_founders / (1 − exp(−N_founders))
      sim.addSubpop("p0", rztpois(1, N_FOUNDERS));
      PARAMS.setValue("FOUNDERS", Dictionary(HOST_ID, c()));
   } else {
      catn("\nSTARTING SIMULATION\n");
      // load input file
      sim.readFromPopulationFile(INFILE);
      // load metadata, which stores information about which individuals are needed to seed this infection
      metadata = treeSeqMetadata(INFILE);
      // determine which individuals to keep
      inds_to_keep = metadata.getValue("FOUNDERS").getValue(HOST_ID);
      PARAMS.setValue("FOUNDERS", metadata.getValue("FOUNDERS"));
      // kill everyone else - mark individuals to be transmitted with a unique tag
      p0.individuals.tagL0 = T;
      sim.individualsWithPedigreeIDs(inds_to_keep).tagL0 = F;
      sim.killIndividuals(p0.subsetIndividuals(tagL0 = T));
   }
   // track simulation start day
   defineConstant("SIM_START", sim.cycle);		
   PARAMS.setValue("SIM_START", SIM_START);
   // calculate the overall transmission day
   defineConstant("ABS_TRANSMISSION_DAY", SIM_START + asInteger(TRANSMISSION_DAY));
   PARAMS.setValue("ABS_TRANSMISSION_DAY", ABS_TRANSMISSION_DAY);
}

// Pathogens are transmitted to next host
ABS_TRANSMISSION_DAY first() {
	// parse 'output_host_id' to figure out which hosts to transmit to
	hosts_to_infect = strsplit(OUTPUT_HOST_IDS, sep = ";");
	// determine how many pathogens are needed to infect all hosts
	N_pathogens = rztpois(length(hosts_to_infect), N_FOUNDERS);
	// determine mutational status of all pathogens
	mut_status = p0.individuals.countOfMutationsOfType(m1);
	weights = 1 + (mut_status * TRANSMISSION_BENEFIT); // carrying the mutation confers X% higher probability of being transmitted
	transmitted_inds = sample(p0.individuals, size = sum(N_pathogens), replace = F, weights=weights);
	sim.treeSeqRememberIndividuals(transmitted_inds); // permanently remember these individuals in subsequent parallel infection steps
	// kill everyone else - mark individuals to be transmitted with a unique tag
	p0.individuals.tagL0 = T;
	transmitted_inds.tagL0 = F;
	sim.killIndividuals(p0.subsetIndividuals(tagL0 = T));
	// construct dictionary of infected hosts and viral genomes to be transmitted to each
	founders = PARAMS.getValue("FOUNDERS");
	for (host in hosts_to_infect, count in N_pathogens, range_end in cumSum(N_pathogens)) {
		sampled_inds = transmitted_inds[(range_end-count):(range_end-1)];
		founders.setValue(host, sampled_inds.pedigreeID);
	}
	// transmit - write out a tree sequence containing the information needed for all subsequent infections
	sim.treeSeqOutput(path = OUTPUT_DIR + INF_ID + "_" + HOST_ID + "_on_inf_day_" + asString(sim.cycle - SIM_START) + "_on_overall_day_" + asString(sim.cycle - 1) + ".trees", simplify=T, includeModel=F, metadata=PARAMS);
	sim.simulationFinished();
}

// Pathogens replicate within the host
reproduction() {
	// choose the target population size based on the specified population growth rate
	target_pop_size = rztpois(1, p0.individualCount * (1 + POP_GROWTH_RATE));
	mut_status = p0.individuals.countOfMutationsOfType(m2);
	num_offspring = rpois(p0.individualCount, POP_GROWTH_RATE * (1 + mut_status * REPRODUCTION_BENEFIT));
	for (i in p0.individuals, n in num_offspring) {
		offspring = p0.addCloned(i, count=n);
	}
	self.active = 0;
}

// Individuals have their fitness scaled according to the overall population size (logistic growth)
early() {
	p0.fitnessScaling = pmin(0.9, K / p0.individualCount);
}

early() {
	catn("Mean m1 frequency: " + mean(p0.individuals.countOfMutationsOfType(m1)));
	catn("Mean m2 frequency: " + mean(p0.individuals.countOfMutationsOfType(m2)));
}


function (void)setupParams(object<Dictionary>$ defaults)
{
	for (k in defaults.allKeys) {
		if (!exists(k))
			defineConstant(k, defaults.getValue(k));
		else
			defaults.setValue(k, executeLambda(k + ";"));
	}
	catn("===========================");
	catn("Model constants: " + defaults.serialize("pretty"));
	catn("===========================");
}
